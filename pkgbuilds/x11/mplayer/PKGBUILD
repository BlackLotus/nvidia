# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=mplayer
pkgver=28583
pkgrel=1
pkgdesc="A movie player for linux"
arch=(i686 x86_64)
depends=('libxxf86dga' 'libxv' 'libmad' 'giflib' 'cdparanoia'
         'sdl' 'lame' 'libtheora' 'xvidcore' 'zlib'
         'libgl' 'smbclient' 'libcaca' 'alsa-lib'
         'x264>=20080625' 'faac' 'ttf-dejavu' 'fribidi')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'live-media' 'libdca' 'mesa')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=()
md5sums=()
_svntrunk=svn://svn.mplayerhq.hu/mplayer/trunk
_svnmod=mplayer
build() {
  cd ${srcdir}
  msg "Updating SVN entries for $_svnmod ..."
  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  rm -rf ${srcdir}/${_svnmod}-build
  cp -r ${_svnmod} ${_svnmod}-build
  cd ${srcdir}/${_svnmod}-build
  # Custom CFLAGS break the mplayer build
  unset CFLAGS
  ./configure --prefix=/usr --disable-gui --disable-arts --enable-x11 \
      --enable-runtime-cpudetection --confdir=/etc/mplayer --disable-nas \
      --enable-gl --enable-tv-v4l1 --enable-tv-v4l2 --enable-largefiles \
      --disable-liblzo --disable-speex --disable-openal \
      --enable-fribidi --disable-libdv --disable-musepack \
      --language=en --disable-dvdnav --disable-esd --disable-mga \
      --disable-libamr_nb --disable-jack --disable-pulse \
      --with-extraincdir=/usr/lib/live-media --disable-aa \
      --disable-lirc --disable-lircc --enable-caca --enable-alsa \
      --disable-ossaudio
  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak
  make || return 1
  make -j1 DESTDIR=${pkgdir} install
  cp etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
  mkdir "${pkgdir}"/usr/share/mplayer
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
  rm -rf ${pkgdir}/usr/share/mplayer/font
}
