# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>

# Thanks to dibblethewrecker for the original work and packages.
# Thanks to testing monkey S1G1 for contributions

pkgname=catalyst-utils
pkgver=8.12
pkgrel=1
pkgdesc="Proprietary AMD/ATI userspace tools and libraries for Radeon brand cards."
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom') 
#finger required for acpi scripts
depends=('xorg-server>=1.1.1' 'libstdc++5' 'netkit-bsd-finger' 'libxcursor')
replaces=('ati-drivers' 'ati-fglrx-utils' 'fglrx-utils')
conflicts=('libgl' 'ati-drivers-module' 'ati-drivers' 'nvidia') 
provides=('libgl')
install=${pkgname}.install
source=(http://www2.ati.com/drivers/linux/ati-driver-installer-${pkgver/./-}-x86.x86_64.run \
        amdcccle.desktop catalyst.sh atieventsd.sh ati-powermode.sh.patch)
md5sums=('2a62d8c5173f091379e458371782a580'
         '1f4b870e977b155af549442932e8ce6f'
         'bdafe749e046bfddee2d1c5e90eabd83'
         'f729bf913613f49b0b9759c246058a87'
         'b157eecaf2e26b386c427f3a64672dbc')
options=('!strip')

build() {
    cd ${srcdir}

    [ "$CARCH" = "i686" ] && _arch="x86";
    [ "$CARCH" = "x86_64" ] && _arch="x86_64";

    /bin/sh ./ati-driver-installer-${pkgver/./-}-x86.x86_64.run --extract archive_files

    # Grab the example ACPI scripts for lid close and AC plugged/unplugged
    mkdir -p ${srcdir}/etc/
    cp ${srcdir}/archive_files/common/usr/share/doc/fglrx/examples/etc/acpi ${srcdir}/etc -r
    cd ${srcdir}/etc/acpi
    sed -i "s#/usr/X11R6/bin/aticonfig#/usr/bin/aticonfig#g" ${srcdir}/etc/acpi/ati-powermode.sh || return 1
    sed -i "s#--effective=now##g" ${srcdir}/etc/acpi/ati-powermode.sh || return 1
    patch -Np0 -i ${srcdir}/ati-powermode.sh.patch
    cd ../..
 
    # Get our architecture files and common files all in one place
    cp ${srcdir}/archive_files/arch/${_arch}/* ${srcdir}/ -r
    cp ${srcdir}/archive_files/common/* ${srcdir}/ -r
    if [ "$CARCH" = "x86_64" ]; then
       cp ${srcdir}/archive_files/x710_64a/* ${srcdir}/ -r
     else
       cp ${srcdir}/archive_files/x710/* ${srcdir}/ -r
    fi

    # Remove src and documents - install remaining files
    mkdir -p ${srcdir}/usr/share/pixmaps
    mv ${srcdir}/usr/share/icons/* ${srcdir}/usr/share/pixmaps/
    rm -rf ${srcdir}/usr/{src,share/{icons,doc,applnk,gnome}}

    # Install into correct paths for Xorg7
    install -d -m 755 ${pkgdir}/usr/include ${pkgdir}/usr/bin \
            ${pkgdir}/usr/lib/xorg/modules ${pkgdir}/etc \
            ${pkgdir}/usr/share ${pkgdir}/usr/sbin \
            ${pkgdir}/etc/rc.d ${pkgdir}/etc/acpi

    mv ${srcdir}/etc/* ${pkgdir}/etc/
    mv ${srcdir}/usr/sbin/* ${pkgdir}/usr/sbin/
    mv ${srcdir}/usr/include/* ${pkgdir}/usr/include/
    mv ${srcdir}/usr/share/* ${pkgdir}/usr/share/
    mv ${srcdir}/usr/X11R6/include/* ${pkgdir}/usr/include/
    mv ${srcdir}/usr/X11R6/bin/* ${pkgdir}/usr/bin/
    if [ "$CARCH" == "x86_64" ]; then
      mv ${srcdir}/usr/X11R6/lib64/modules/* ${pkgdir}/usr/lib/xorg/modules/
      rm -rf ${srcdir}/usr/X11R6/lib64/modules
      mv ${srcdir}/usr/X11R6/lib64/* ${pkgdir}/usr/lib
     else 
      mv ${srcdir}/usr/X11R6/lib/modules/* ${pkgdir}/usr/lib/xorg/modules/
      rm -rf ${srcdir}/usr/X11R6/lib/modules
      mv ${srcdir}/usr/X11R6/lib/* ${pkgdir}/usr/lib/
    fi

   # correct dir permissions
    find ${pkgdir}/usr -type d -exec chmod 755 {} \;

    # correct FILE permissions - >=8.39.4 has nearly every file marked 0744. This
    # is dumb, and previous versions of the drivers had no files marked this
    # way.  Fixing permissions of files to what they were in 8.38.6
    find ${pkgdir}/etc -not -type d -exec chmod 0444 {} \;
    find ${pkgdir}/etc -name '*.sh' -not -type d -exec chmod a+x {} \;
    chmod u+w ${pkgdir}/etc/{ati/control,acpi/ati-powermode.sh}
    find ${pkgdir}/usr/{bin,lib,sbin} -not -type d -exec chmod 0755 {} \;
    chmod 0555 ${pkgdir}/usr/sbin/atigetsysteminfo.sh
    find ${pkgdir}/usr/lib -name '*.a' -not -type d -exec chmod 0644 {} \;
    find ${pkgdir}/usr/{share,include} -not -type d -exec chmod 0444 {} \;
    find ${pkgdir}/usr/share/ati/amdcccle/ -name '*.qm' -not -type d -exec chmod 0644 {} \;
    find ${pkgdir}/usr/share/man -not -type d -exec chmod 0644 {} \;

    # create proper symlinks to libGL.so, libfglrx_pp.so, and libfglrx_gamma.so
    cd ${pkgdir}/usr/lib
    ln -s libGL.so.1.2 libGL.so
    ln -s libGL.so.1.2 libGL.so.1
    ln -sf /usr/lib/libfglrx_pp.so.1.0 libfglrx_pp.so.1
    ln -sf /usr/lib/libfglrx_gamma.so.1.0 libfglrx_gamma.so.1

    mkdir -p ${startdir}/pkg/usr/lib/xorg/modules/extensions
    cd ${startdir}/pkg/usr/lib/xorg/modules/extensions
    ln -sf libGLcore.xorg libGLcore.so
    ln -sf libglx.xorg libglx.so
    
    # install licenses
    install -m 0644 -D ${srcdir}/archive_files/ATI_LICENSE.TXT \
                     ${pkgdir}/usr/share/licenses/${pkgname}/AMD_ATI_LICENSE.TXT
    # create DE file
    install -m 0755 -D ${srcdir}/amdcccle.desktop \
            ${pkgdir}/usr/share/applications/amdcccle.desktop
    # thanks to cerebral, we dont need that damned symlink
    install -m 0755 -D ${srcdir}/catalyst.sh \
            ${pkgdir}/etc/profile.d/catalyst.sh
    # Add ATI Events Daemon launcher
    install -m 0755 -D ${srcdir}/atieventsd.sh \
            ${pkgdir}/etc/rc.d/atieventsd

}

