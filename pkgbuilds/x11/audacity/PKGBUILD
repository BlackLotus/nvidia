# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: dorphell <dorphell@archlinux.org>
pkgname=audacity
pkgver=1.3.6
pkgrel=4
pkgdesc="Audacity is a program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'libsamplerate' \
         'lame' 'shared-mime-info' 'desktop-file-utils' 'redland')
makedepends=('zip' 'python' 'pkgconfig' 'soundtouch')
options=('!makeflags')
install=audacity.install
source=(http://downloads.sourceforge.net/sourceforge/audacity/${pkgname}-src-${pkgver}.tar.bz2 \
        audacity.xpm audacity16.xpm audacity-1.3.4-automagick.patch)
md5sums=('a59d30bcedd017c5b3ac7bcecf7a1628'
         'c8f7f7851638eedf84d05a1afde797b2'
         '5e94ccbfb665e8356dc4172a3673b4d4'
         'cd1419f34ee7044e3ccf315faf04d22a')
build() {
  cd ${srcdir}/${pkgname}-src-${pkgver}
  # force a sane build system, kudos to gentoo
  patch -Np1 -i "${srcdir}"/audacity-1.3.4-automagick.patch
  WX_CONFIG=/usr/bin/wx-config \
  ./configure --prefix=/usr \
     --with-portaudio=v19 --with-libsamplerate=system \
     --without-libresample --with-libmad=system \
     --with-id3tag=system --with-flac=system \
     --with-vorbis=system --with-libexpat=system \
     --with-libsndfile=system --with-soundtouch \
     --without-jack \
     --enable-unicode || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  install -m644 ../audacity.xpm ${pkgdir}/usr/share/${pkgname}/audacity.xpm || return 1
  install -m644 ../audacity16.xpm ${pkgdir}/usr/share/${pkgname}/audacity16.xpm || return 1
}
