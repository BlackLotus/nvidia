# Contributor: Shino <shino@jenux.homelinux.org>
pkgname=kismet-newcore-svn
pkgver=2538
pkgrel=1
pkgdesc="Next generation of the wireless network sniffer"
arch=('i686' 'x86_64')
url="http://www.kismetwireless.net/"
license=('GPL')
depends=('libpcap' 'libcap' 'libnl' 'pcre')
makedepends=('subversion')
provides=(kismet kismet-newcore)
conflicts=(kismet kismet-newcore)
source=()
md5sums=() 

_svntrunk="http://svn.kismetwireless.net/code/branch/kismet-newcore"
_svnmod="kismet-newcore"

build() {
  cd "${srcdir}"
  msg "Connecting to SVN server...."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  cd $_svnmod
  
  # Building
  ./configure --prefix /usr/ --sysconfdir /etc/
  make || return 1

  # Installing
  make DESTDIR="${pkgdir}" install || return 1

  # Installing the headers (needed when compiling plugins)
  find . -name '*.h' -exec install -D -m644 "{}" "${pkgdir}/usr/include/kismet/{}" \;
}
