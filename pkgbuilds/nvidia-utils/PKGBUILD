# $Id: PKGBUILD 14561 2008-10-08 11:29:43Z pierre $
# Maintainer: Thomas Baechler <thomas@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>

pkgname=nvidia-utils
pkgver=177.82
pkgrel=1
pkgdesc="NVIDIA drivers utilities and libraries."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86 	 
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
depends=('xorg-server')
conflicts=('libgl' 'libgl-dri' 'ati-fglrx-utils' 'nvidia-legacy-utils' \
           'nvidia-71xx-utils' 'nvidia-96xx-utils')
provides=('libgl')
license=('custom')
install=nvidia.install
source=(http://us.download.nvidia.com/XFree86/Linux-${ARCH}/${pkgver}/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0.run \
        supported-cards.txt)
md5sums=('882f34a662f0175682fdc4a9bec909ba'
         'bf8d68d66757096c3ab17653230bf2a8')
[ "$CARCH" = "x86_64" ] && md5sums=('bab63757e04eb3ee11129a281cb11afd'
                        	    'bf8d68d66757096c3ab17653230bf2a8')

options=(docs !strip)

build()
{
  # override nvida install routine and do it the long way.
  cd ${srcdir}/
  sh NVIDIA-Linux-${ARCH}-${pkgver}-pkg0.run --extract-only
  cd NVIDIA-Linux-${ARCH}-${pkgver}-pkg0/usr/

  mkdir -p ${pkgdir}/usr/{lib,bin,share/applications,share/pixmaps,man/man1}
  mkdir -p ${pkgdir}/usr/lib/xorg/modules/{extensions,drivers}
  mkdir -p ${pkgdir}/usr/share/licenses/nvidia/
  
  install lib/{libGLcore,libGL,libnvidia-cfg,libcuda,tls/libnvidia-tls}.so.${pkgver} \
       ${pkgdir}/usr/lib/ || return 1
  install -m644 share/man/man1/* ${pkgdir}/usr/man/man1/ || return 1
  rm ${pkgdir}/usr/man/man1/nvidia-installer.1.gz || return 1
  install X11R6/lib/libXv* ${pkgdir}/usr/lib/ || return 1
  install -m644 share/applications/nvidia-settings.desktop ${pkgdir}/usr/share/applications/ || return 1
  # fix nvidia .desktop file
  sed -e 's:__UTILS_PATH__:/usr/bin:' -e 's:__PIXMAP_PATH__:/usr/share/pixmaps:' -i ${pkgdir}/usr/share/applications/nvidia-settings.desktop
  install -m644 share/pixmaps/nvidia-settings.png ${pkgdir}/usr/share/pixmaps/ || return 1
  #install X11R6/lib/modules/libnvidia-wfb.so.$pkgver ${pkgdir}/usr/lib/xorg/modules || return 1
  install X11R6/lib/modules/drivers/nvidia_drv.so ${pkgdir}/usr/lib/xorg/modules/drivers || return 1
  install X11R6/lib/modules/extensions/libglx.so.$pkgver ${pkgdir}/usr/lib/xorg/modules/extensions || return 1
  install -m755 bin/nvidia-{settings,xconfig,bug-report.sh} ${pkgdir}/usr/bin/ || return 1
  cd ${pkgdir}/usr/lib/
  ln -s libGL.so.$pkgver libGL.so || return 1
  ln -s libGL.so.$pkgver libGL.so.1 || return 1
  ln -s libGLcore.so.$pkgver libGLcore.so.1 || return 1
  ln -s libnvidia-cfg.so.$pkgver libnvidia-cfg.so.1 || return 1
  ln -s libnvidia-tls.so.$pkgver libnvidia-tls.so.1 || return 1
  ln -s libcuda.so.$pkgver libcuda.so.1 || return 1
  ln -s libXvMCNVIDIA.so.$pkgver libXvMCNVIDIA_dynamic.so.1 || return 1
  cd ${pkgdir}/usr/lib/xorg/modules/extensions
  ln -s libglx.so.$pkgver libglx.so || return 1

  install -m644 ${srcdir}/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0/LICENSE ${pkgdir}/usr/share/licenses/nvidia/ || return 1
  ln -s nvidia ${pkgdir}/usr/share/licenses/nvidia-utils || return 1
  install -D -m644 ${srcdir}/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0/usr/share/doc/README.txt ${pkgdir}/usr/share/doc/nvidia/README || return 1
  install -D -m644 ${srcdir}/supported-cards.txt ${pkgdir}/usr/share/doc/nvidia/supported-cards.txt || return 1
  
  find ${pkgdir}/usr -type d -exec chmod 755 {} \;
  # phew :)
}
