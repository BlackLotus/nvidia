# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
pkgname=wine
pkgver=1.1.10
pkgrel=1
pkgdesc="A compatibility layer for running Windows programs"
url="http://www.winehq.com"
arch=('i686')
license=('LGPL')
depends=('fontconfig' 'libldap' 'libxslt' 'lcms' 'libxxf86dga' \
         'libxcursor' 'libxrandr' 'libxdamage')
makedepends=('cups' 'sane' 'fontforge' 'flex' 'bison' 'libgphoto2' 
             'libxinerama' 'esd' 'giflib' 'ncurses' 'libxcomposite' 'samba' \
             'libxpm' 'libxmu' 'libxxf86vm' 'perl' 'libxml2')
optdepends=("cups" "sane" "alsa-lib" "giflib" "libjpeg" "libpng" \
           "perl" "libxml2" "libgphoto2")
source=(http://downloads.sourceforge.net/wine/wine-$pkgver.tar.bz2)
md5sums=('939ddfacb80678a99b3c960edb3a30f5')

build() {
  CFLAGS="$CFLAGS -Os"
  CXXFLAGS="$CFLAGS"
  cd "${srcdir}"/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --with-x --without-capi --without-esd \
  --without-jack
  make depend || return 1
  make || return 1
  make prefix="${pkgdir}"/usr install || return 1
  mkdir -p "${pkgdir}"/etc/wine
}
