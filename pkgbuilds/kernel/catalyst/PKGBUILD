# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: amdviaman

pkgname=catalyst
pkgver=9.1
pkgrel=1
pkgdesc="Proprietary AMD/ATI kernel drivers for Radeon brand cards. Stock kernel."
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom') 
depends=("catalyst-utils>=${pkgver}")
makedepends=()
replaces=('ati-fglrx' 'fglrx') # Yay rebranding
install=${pkgname}.install
source=(http://www2.ati.com/drivers/linux/ati-driver-installer-${pkgver/./-}-x86.x86_64.run)
md5sums=('cc4ff401586a6c099c9fd002c25cf7bb')

build() {
    source "${startdir}"/../kernel.env
    cd "${srcdir}"
    if [ "${CARCH}" = "x86_64" ]; then
    	BUILDARCH=x86_64
    	_archdir=x86_64
    fi
    if [ "${CARCH}" = "i686" ]; then
    	BUILDARCH=i386
    	_archdir=x86
    fi
    /bin/sh ./ati-driver-installer-${pkgver/./-}-x86.x86_64.run --extract archive_files
    cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
    cp "${srcdir}/archive_files/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a.GCC4" . || return 1
    # Build the kernel module
    cp 2.6.x/Makefile .
    make -C $_kerndir SUBDIRS="`pwd`" ARCH=$BUILDARCH modules || return 1
    # Install the kernel module
    install -m755 -d "${pkgdir}/lib/modules/${_kernver}/video/"
    install -m644 fglrx.ko "${pkgdir}/lib/modules/${_kernver}/video/" || return 1
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 "${srcdir}/archive_files/ATI_LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
    sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" $startdir/*.install
}

