# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# toolchain build order: kernel-headers->glibc->binutils->gcc-libs->gcc->binutils->glibc

pkgname=binutils
pkgver=2.19
pkgrel=1
_date=20081119
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="http://sources.redhat.com/binutils"
license=('GPL')
groups=('base')
options=('!libtool' '!distcc' '!ccache')
makedepends=('texinfo')
depends=('zlib')
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}_${_date}.tar.bz2)
md5sums=('4a49694b08ef0e5931dba4813326d062')
install=binutils.install

build() {
  # for cvs checkout
#  mkdir "${srcdir}"/${pkgname}-${_date}
#  cd "${srcdir}"/${pkgname}-${_date}
#  export _TAG=binutils-2_19-branch
#  export 'CVSROOT=:pserver:anoncvs@sourceware.org:/cvs/src'
#  cvs -z9 co -r $_TAG binutils || return 1
#  cd src && tar -cvjf ../../../binutils-2.19_${_date}.tar.bz2 *
#  return 1

  cd "${srcdir}"

  mkdir build
  cd build

  CC="gcc -L`pwd`/bfd/.libs/"
  if [ "${CARCH}" = "x86_64" ]; then
    ../configure --prefix=/usr --infodir=/usr/share/info --mandir=/usr/share/man --enable-shared --enable-64-bit-bfd --disable-multilib 
  else
    ../configure --prefix=/usr --infodir=/usr/share/info --mandir=/usr/share/man --enable-shared --without-included-gettext
  fi
  # fix info files install path
  sed -i -e "s:infodir \= \/usr\/share:infodir \= ${pkgdir}\/usr\/share:" ${srcdir}/build/Makefile || return 1

  # This checks the host environment and makes sure all the necessary tools are available to compile Binutils.
  make configure-host || return 1

  make tooldir="${pkgdir}"/usr || return 1
  make prefix="${pkgdir}"/usr tooldir="${pkgdir}"/usr install
  install -m 644 ../include/libiberty.h "${pkgdir}"/usr/include

  if [ "${CARCH}" = "x86_64" ]; then
    # Rebuild libiberty.a with -fPIC
    make -C libiberty clean
    make CFLAGS="$CFLAGS -fPIC" -C libiberty
    install -m 644 libiberty/libiberty.a "${pkgdir}"/usr/lib
    # Rebuild libbfd.a with -fPIC
    make -C bfd clean
    make CFLAGS="$CFLAGS -fPIC" -C bfd
    install -m 644 bfd/libbfd.a "${pkgdir}"/usr/lib
  fi

  rm -f "${pkgdir}"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # Remove these symlinks, with binutils prereleases they are not ABI stable.
  # Programs should compile static to the .a file.
  rm -f "${pkgdir}"/usr/lib/lib{bfd,opcodes}.so

  rm -f "${pkgdir}"/usr/share/info/dir
}
