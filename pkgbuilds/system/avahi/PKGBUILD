# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=avahi
pkgver=0.6.24
pkgrel=1
pkgdesc="A multicast/unicast DNS-SD framework"
arch=('i686' 'x86_64')
url="http://www.avahi.org/"
license=('LGPL')
depends=('libglade' 'libcap' 'libdaemon>=0.11' 'python' 'dbus-core')
optdepends=('libglade:Avahi-discover-standalone'
            'nss-mdns:NSS support for mDNS')
makedepends=('mono' 'pygtk' 'gtk-sharp-2' 'dbus-python' 'libglade' 'intltool')
backup=(etc/avahi/avahi-daemon.conf)
install=avahi.install
conflicts=('howl' 'mdnsresponder')
provides=('howl' 'mdnsresponder')
replaces=('howl' 'mdnsresponder')
options=('!libtool')
source=(http://www.avahi.org/download/avahi-${pkgver}.tar.gz gnome-nettool.png)

build() {
  export MONO_SHARED_DIR="${srcdir}"/.wabi
  mkdir -p ${MONO_SHARED_DIR}

  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-qt4 \
    --disable-qt3 \
    --disable-monodoc \
    --disable-doxygen-doc \
    --disable-xmltoman \
    --enable-compat-libdns_sd \
    --enable-compat-howl \
    --with-distro=archlinux \
    --with-avahi-priv-access-group=network \
    --enable-autoipd \
    --with-autoipd-user=avahi \
    --with-autoipd-group=avahi \
    --disable-gdbm

  make || return 1
  make DESTDIR="${pkgdir}" install

  rm -rf ${MONO_SHARED_DIR}
  
  #fix capability
  sed -i -e 's|$DAEMON -D |modprobe capability  > /dev/null 2>\&1 ;  $DAEMON -D |' "${pkgdir}"/etc/rc.d/avahi-daemon

  sed -i -e 's/netdev/network/g' "${pkgdir}"/etc/dbus-1/system.d/avahi-dbus.conf
   
  # howl and mdnsresponder compatability
  cd "${pkgdir}"/usr/include
  ln -s avahi-compat-libdns_sd/dns_sd.h dns_sd.h
  ln -s avahi-compat-howl howl
  cd "${pkgdir}"/usr/lib/pkgconfig
  ln -s avahi-compat-howl.pc howl.pc
  mkdir -p "${pkgdir}"/usr/share/pixmaps
  install -m 644 "${srcdir}"/gnome-nettool.png "${pkgdir}"/usr/share/pixmaps/gnome-nettool.png
}

md5sums=('068c1d220b07037e64caf87d4a7a0504'
         '42c2905307c7a5dc6ac4b75f4c3d65a3')

