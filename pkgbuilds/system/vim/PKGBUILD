# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: tobias [ tobias at archlinux org ]

pkgname=vim
_srcver=7.2
_patchlevel=69
pkgver=${_srcver}.${_patchlevel}
pkgrel=1
pkgdesc="a highly configurable, improved version of the vi text editor"
arch=(i686 x86_64)
license=('custom:vim')
url="http://www.vim.org"
depends=("vi>=${pkgver}" 'perl' 'python' 'ruby' 'acl' 'gpm>=1.20.4')
makedepends=('wget' 'sed' 'grep')
backup=(etc/vimrc)
install=${pkgname}.install
# we need the extra-stuff to get all patches applied smoothly
source=(ftp://ftp.vim.org/pub/vim/unix/vim-${_srcver}.tar.bz2 \
        ftp://ftp.vim.org/pub/vim/extra/vim-${_srcver}-extra.tar.gz \
        ftp://ftp.vim.org/pub/vim/extra/vim-${_srcver}-lang.tar.gz \
        fetch_patches.sh)
md5sums=('f0901284b338e448bfd79ccca0041254'
         '35e04482f07c57221c9a751aaa3b8dac'
         'd8884786979e0e520c112faf2e176f05'
         'a3b03cd44b8ed78a99850d4cbfaafe55')
build()
{
  # pull in patches from vim.org (or the src cache alternatively)
  . "${srcdir}"/fetch_patches.sh
  get_patches || return 1
  cd "${srcdir}"/vim$(echo ${_srcver} | sed "s/\.//")
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' src/feature.h
  # build party
  ./configure --prefix=/usr --localstatedir=/var/lib/vim --mandir=/usr/share/man \
    --with-compiledby=ArchLinux --with-features=big \
    --with-x=no --disable-gui \
    --with-global-runtime=/usr/share/vim --with-vim-name=vim \
    --enable-multibyte --enable-cscope \
    --enable-perlinterp --enable-pythoninterp --enable-rubyinterp
  make || return 1
  make  VIMRCLOC=/etc DESTDIR="${pkgdir}" VIMRTDIR= install
  cd "${pkgdir}"/usr/bin
  rm -f ex view rview xxd vimtutor
  ln -s vitutor vimtutor

   # delete the manpages/symlinks provided by vi package
  find "${pkgdir}"/usr/share/man -type d -name 'man1' 2> /dev/null | \
   while read mandir; do
    cd ${mandir}
    rm -f *.1
    ln -s rvi.1.gz rvim.1.gz
    ln -s vi.1.gz vim.1.gz
    ln -s vidiff.1.gz vimdiff.1.gz
    ln -s vitutor.1.gz vimtutor.1.gz
  done

  # kill the nobackup parts
  sed -i '/vms/,+4 d' "${pkgdir}"/usr/share/vim/vimrc_example.vim
  install -Dm644 "${pkgdir}"/usr/share/vim/vimrc_example.vim \
    "${pkgdir}"/etc/vimrc
  # clean all settings and controls -  served by vi package
  rm -rf "${pkgdir}"/usr/share/vim
}
