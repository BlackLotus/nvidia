# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
pkgname=libarchive
pkgver=2.6.0
pkgrel=1
pkgdesc="library that can create and read several streaming archive formats"
arch=(i686 x86_64)
url="http://people.freebsd.org/~kientzle/libarchive/"
license=('BSD')
groups=('base')
depends=('zlib' 'bzip2' 'acl')
provides=("tar")
conflicts=("tar")
replaces=("tar")
source=(http://people.freebsd.org/~kientzle/libarchive/src/libarchive-$pkgver.tar.gz)
md5sums=('e8ceea99a86b022e192a06d2b411a29b')
# pacman.static build fails unless we keep the libtool files (or unless we link
# the missing symbols inside the libarchive .a static lib, but that is dirty)
options=(libtool)
build() {
  cd "${srcdir}"/$pkgname-$pkgver
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR="${pkgdir}" install
  # install license
  mkdir -p "${pkgdir}"/usr/share/licenses/libarchive
  install -m644 COPYING "${pkgdir}"/usr/share/licenses/libarchive/
  # make symlink for programs expecting gnu tar
  mkdir "${pkgdir}"/bin
  cd "${pkgdir}"/bin
  ln -s ../usr/bin/bsdtar tar
}

